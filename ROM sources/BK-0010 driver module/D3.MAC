




;  ************************************************
;  *                                              *
;  *    д п ю и б е п    T V - л н м х р н п ю    *
;  *                                              *
;  ************************************************



;   =======================
;   *** йнмярюмрш DTVMN ***
;   =======================

	ARRUL	= 177664	;- юдпея пецхярпю пскнмю
	NZRUL0	= 1330		;- мюв. гмювемхе RRUL б ням.пеф.
	NZRUL1	= 230		;- мюв. гмювемхе RRUL б пеф. по

	KSSTR	= 100		;- йнкхв. яхлбнкнб б ярпнйе
	KTVSS	= 12		;- йнкхв. TV ярпнй б яхлб. ярпнйе
	KTVSSS	= 20		;- йнкхв. TV ярпнй б яксф. ярпнйе

	DTVSTB	= 100		;- дкхмю TV ярпнйх
	DTVSTS	= 40
	DSSTRB	= 1200		;- дкхмю яхлбнкэмни ярпнйх
	DSSTRS	= 500
	DSLSTB	= 2000		;- дкхмю яксфеамни ярпнйх
	DSLSTS	= 1000





; ========================================
; ***** лндскэ тнплхпнбюмхъ яхлбнкнб *****
; ========================================


MFSIM:	;бунд:	R0 - йнд б лк. аюире



;   =================================
;   *** 1. акнй сопюбкемхъ MFSTVM ***
;   =================================


BUMFS:	JSR	R4,PSREG	;  янупюмемхе пецхярпнб
	INC	SCKOD		;- явер йнднб

	TSTB	PRGRAF		;  рейярнбюъ цпютхйю
	BEQ	1$
	JSR	PC,BTG
	BR	KBUMFS

1$:	BIC	#177400,R0	;  цпютхвеяйхе яхлбнкш
	CMPB	R0,#177
	BHI	2$
	CMPB	R0,#37
	BLOS	3$
	SUB	#20,R0
5$:	JSR	PC,BGS
	BR	KBUMFS
2$:	CMPB	R0,#237
	BLOS	4$
	SUB	#60,R0
	BR	5$

3$:	CMPB	R0,#21		;  педюйрхпсчыхе яхлбнкш
	BLOS	6$
7$:	JSR	PC,BRS
	BR	KBUMFS

6$:	CMPB	R0,#10		;  сопюбкъчыхе яхлбнкш
	BEQ	7$
	JSR	PC,BUS
	BR	KBUMFS

4$:	JSR	PC,BFS		;  тсмйжхнмюкэмше яхлбнкш

KBUMFS:	JSR	R4,PWREG
	RTS	PC



;   ====================================
;   *** 2. акнй цпютхвеяйху яхлбнкнб ***
;   ====================================


BGS:	JSR	PC,PFSIM	;  тнплхпнбюмхе яхлбнкю

	ADD	DSIMB,NOMSIM	;  хглемемхе юдпеяю яхлбнкю
	JSR	PC,PFASIM

	JSR	PC,PFSK		;  тнплхпнбюмхе йспянпю

	RTS	PC


;    --------------------------------------
;    ** 2.1. тнплхпнбюмхе юдпеяю яхлбнкю **
;    --------------------------------------

PFASIM:	TSTB	R5		;  опхгмюй опнхгб. юдпеяю
	BEQ	1$

	BIT	#77,NOMSIM	;  опнбепйю мювюкю ярпнйх
	BNE	2$

1$:	TSTB	PRNRUL		;  бняяр. яксф. ярпнйх
	BEQ	8$
	JSR	PC,PFSSTR
	CLRB	PRNRUL

8$:	CLR	R3		;  нопед. мюопюбк. дбхф. йспя.
	MOV	NOMSIM,R5
	BPL	3$
	TST	(R3)+		;- ббепу
6$:	ADD	#KSSTR,R5
	BMI	6$
	BR	4$
3$:	CMP	R5,KSVP		;- бмхг
	BLO	5$
7$:	SUB	#KSSTR,R5
	CMP	R5,KSVP
	BHIS	7$

4$:	JSR	PC,PRUL		;  нрпюанрйю пскнмю

5$:	MOV	R5,NOMSIM	;  бшвхякемхе AS
	MOV	R5,R4
	BIC	#177700,R4
	BIC	#77,R5
	ASL	R5
	ADD	R5,R4
	ASL	R5
	ASL	R5
	ADD	R5,R4
	ADD	BAZVP,R4
	BIC	#140000,R4
	ADD	ANVP,R4
	MOV	R4,AS
	BR	KFASIM

2$:	ADD	DSIMB,AS	;  бшвхякемхе AS б ярпнйе

KFASIM:	RTS	PC


;    -----------------------------
;    ** 2.1.1. нрпюанрйю пскнмю **
;    -----------------------------

PRUL:	TSTB	PRRP		;  опхгмюй пюяьхпеммни оюлърх
	BNE	1$

	MOV	KRUL1(R3),R4	;  ядбхц яксф. ярпнйх
	MOV	BAZVP,R1
	MOV	#KTVSSS,R2
	MOV	#DSSTRB,-(SP)
	TST	R3
	BEQ	2$
	SUB	#DSLSTB,R1	;- ядбхц ббепу
	NEG	(SP)
2$:	BIC	#140000,R1
	ADD	ANVP,R1
	INCB	PRNRUL
	JSR	PC,(R4)

	MOV	#KTVSS,R4	;  ядбхц щйпюмю
	MOV	(SP)+,R1
	BPL	3$
	NEGB	R4		;- ядбхц бмхг
3$:	ADD	BAZVP,R1
	BIC	#140000,R1
	MOV	R1,BAZVP
	ADD	R4,@#ARRUL
	DECB	PRNRUL
	BR	KRUL

1$:	MOV	#36,R2		;  пефхл пюяьхп. оюлърх
	MOV	#73200,R1	;- ядбхц щйпюмю ббепу
	NEG	R3
	BEQ	4$
	MOV	#75600,R1	;- ядбхц щйпюмю бмхг
4$:	MOV	KRUL2(R3),R4
	JSR	PC,(R4)

KRUL:	RTS	PC
KRUL1:	.WORD	PSDWN
KRUL2:	.WORD	PSDWW


;    -------------------------------
;    ** 2.2. тнплхпнбюмхе яхлбнкю **
;    -------------------------------

PFSIM:	ASL	R0		;  бшвхякемхе юдп. хгнап.
	MOV	R0,R1
	ASL	R0
	ASL	R0
	ADD	R0,R1
	ADD	#IZSIM,R1

	MOV	AS,R3		;  ондц. юдп. х явервхйю
	MOV	#11,R2

	TSTB	PRCW		;  ва/жб
	BNE	PFCWS

;	тнплхпнбюмхе вепмн-аекнцн яхлбнкю
;	---------------------------------

	CLR	R0		;  сяр. опхгм. хмбепяхх
	TSTB	PRINWS
	BEQ	1$
	COM	R0
1$:	TST	FON
	BEQ	2$
	COM	R0

2$:	MOVB	(R1)+,(R3)	;  тнплхпнбюмхе яхлбнкю
	TST	R0
	BEQ	3$
	COMB	(R3)
3$:	JSR	PC,PFTAN
	SOB	R2,2$
	MOVB	(R1)+,(R3)
	TST	R0
	BEQ	4$
	COMB	(R3)

4$:	TSTB	PRPODC		;  ондвепйхбюмхе
	BEQ	KFSIM
	MOVB	MASCW,(R3)
	BR	KFSIM

;	тнплхпнбюмхе жбермнцн яхлбнкю
;	-----------------------------

PFCWS:	CLR	-(SP)

6$:	MOVB	(R1)+,R5	;  ондц. хяу. дюмм. ярпнйх
	BIC	#177400,R5
	MOV	#20,R4
	CLR	R0

	TSTB	PRINWS		;  хмбепяхъ
	BEQ	1$
	COMB	R5

1$:	TSTB	R5		;  пюяьхпемхе ярпнйх
	BEQ	2$
5$:	ASR	R5
	BCC	3$
	BEQ	4$
	BIS	MASPOZ(R4),R0
3$:	SUB	#2,R4
	BNE	5$
4$:	BIS	MASPOZ(R4),R0

2$:	MOV	FON,R5		;  тнплхпнбюмхе жберю
	BIC	R0,R5
	MOV	MASCW,R4
	COM	R0
	BIC	R0,R4
	BIS	R5,R4

	MOV	R4,(R3)		;  гюохяэ ярпнйх яхлбнкю

	JSR	PC,PFTAN	;  йнмеж жхйкю
	SOB	R2,6$

	TST	(SP)		;  бшунд
	BNE	7$
	INC	(SP)
	INC	R2

	TSTB	PRPODC		;  ондвепйхбюмхе
	BEQ	6$
	MOV	MASCW,(R3)

7$:	MOV	(SP)+,R0

KFSIM:	RTS	PC


;    -------------------------------------------
;    ** 2.3. тнплхпнбюмхе яхлбнкэмнцн йспянпю **
;    -------------------------------------------

PFSK:				;  тнплхпнбюмхе х ярхпюмхе яхлб. йспя.
PSTSK:	TSTB	PRGAHK		;  опхгмюй цюь. йспя.
	BNE	KFSK

	MOV	AS,R3		;  ондц. оюпюл.
	MOV	#KTVSS,R2

	TSTB	PRCW		;  ва/жб
	BNE	1$

	MOV	#377,R5		;  вепмн-аекши
	ASR	R3
	BCC	2$
	SWAB	R5
2$:	ASL	R3
	BR	3$

1$:	MOV	MASCW,R5	;  жбермни
	MOV	FON,R4
	XOR	R4,R5

3$:	XOR	R5,(R3)		;  хглемемхе люпйепю
	JSR	PC,PFTAN
	SOB	R2,3$

KFSK:	RTS	PC



;   ======================================
;   *** 3. акнй педюйрхпсчыху яхлбнкнб ***
;   ======================================


BRS:	TSTB	PRBLR		;  пефхл акнй.пед.
	BEQ	1$
	JSR	PC,PFUS1
	BR	KBRS

1$:	MOV	NOMSIM,R1	;  ондц.оюпюл.
	MOV	DSIMB,R2
	MOV	#KSSTR,R3

	CMPB	R0,#10		;  йк
	BNE	2$
	SUB	R2,R1
	BR	3$

2$:	SUB	#22,R0		;  бшанп йндю
	ASL	R0
	MOV	4$(R0),R5
	JSR	PC,(R5)
	RTS	PC
4$:	.WORD	22$
	.WORD	PSTRW
	.WORD	PSTRN
	.WORD	25$
	.WORD	PSIML
	.WORD	PSIMP
	.WORD	PANSIM
	.WORD	31$
	.WORD	32$
	.WORD	33$
	.WORD	34$
	.WORD	35$
	.WORD	36$
	.WORD	37$

22$:	CLR	R1		;  ймщ
	BR	3$

25$:	ADD	R3,R1		;  ймяя
	BIC	#77,R1
	BR	3$

31$:	ADD	R2,R1		;  йо
	BR	3$

32$:	SUB	R3,R1		;  йб
	BR	3$

33$:	ADD	R3,R1		;  йм
	BR	3$

34$:	SUB	R3,R1		;  йкб
	SUB	R2,R1
	BR	3$

35$:	SUB	R3,R1		;  йоб
	ADD	R2,R1
	BR	3$

36$:	ADD	R3,R1		;  йом
	ADD	R2,R1
	BR	3$

37$:	ADD	R3,R1		;  йкм
	SUB	R2,R1

3$:	MOV	R1,NOMSIM	;  педюйрхпнбюмхе йспянпю
	JSR	PC,PREDSK

KBRS:	RTS	PC


;    -------------------------------
;    ** 3.1. юмскхпнбюмхе яхлбнкю **
;    -------------------------------

PANSIM:	JSR	PC,PSTSK
	SUB	DSIMB,NOMSIM
	CLR	R5
	JSR	PC,PFASIM
	JSR	PC,PSBRS
	JSR	PC,PFSK

	RTS	PC


;    ----------------------------------
;    ** 3.2. ялеыемхе яхлбнкнб бкебн **
;    ----------------------------------

PSIML:	MOV	AS,R3		;  ондцнрнбйю юдпеянб
	MOV	R3,R4
	BIS	#76,R4
	BIS	DSIMB,R4
	MOV	#12,R2

3$:	MOV	R3,R1		;  ондцнрнбйю жхйкю
	MOV	R1,R5
	ADD	DSIMB,R5

2$:	BIT	#77,R5		;  ядбхц ярпнйх
	BEQ	1$
	MOVB	(R5)+,(R1)+
	BR	2$

1$:	JSR	PC,PFTAN	;  хглемемхе юдп.ярпнйх
	SOB	R2,3$

	MOV	AS,-(SP)	;  яапня онякедмецн яхлб.
	MOV	R4,AS
	JSR	PC,PSBRS
	MOV	(SP)+,AS

	JSR	PC,PFSK

	RTS	PC


;    -----------------------------------
;    ** 3.4. ялеыемхе яхлбнкнб бопюбн **
;    -----------------------------------

PSIMP:	JSR	PC,PSTSK

	MOV	AS,R3		;  ондц.юдпеянб
	MOV	#12,R2

2$:	MOV	R3,R5		;  ондц.жхйкю
	BIS	#77,R5
	INC	R5
	MOV	R5,R1
	SUB	DSIMB,R1

1$:	MOVB	-(R1),-(R5)	;  ядбхц ярпнйх
	CMP	R5,R3
	BNE	1$

	JSR	PC,PFTAN	;  хглемемхе юдп. ярпнйх
	SOB	R2,2$

	JSR	PC,PSBRS	;  яапня рейсыецн яхлб.

	JSR	PC,PFSK		;  тнплхпнбюмхе йспянпю

	RTS	PC


;    -------------------------------
;    ** 3.4. ялеыемхе ярпнй ббепу **
;    -------------------------------

PSTRW:	MOV	AS,R1		;  юдпея хярнвмхйю
	BIC	#77,R1
	ADD	#DSSTRB,R1
	BPL	1$
	SUB	DVPB,R1

1$:	JSR	PC,PSCTVS	;  нопед.яв.рб.ярпнй

	JSR	PC,PSDWW	;  ядбхц ббепу
	JSR	PC,PFSK

	RTS	PC


;    ------------------------------
;    ** 3.5. ялеыемхе ярпнй бмхг **
;    ------------------------------

PSTRN:	JSR	PC,PSTSK

	MOV	BAZVP,R1	;  юдпея хярнвмхйю + 1
	ADD	DGPB,R1
	SUB	#DSSTRB,R1
	BIC	#140000,R1
	ADD	ANVP,R1

	JSR	PC,PSCTVS	;  нопед. яв. рб. ярпнй

	JSR	PC,PSDWN	;  ядбхц бмхг
	JSR	PC,PFSK

	RTS	PC


;    ---------------------------------------
;    ** 3.6. бшвхякемхе явервхйю TV ярпнй **
;    ---------------------------------------

PSCTVS:	MOV	KSVP,R3		;  дкхмю онкъ
	MOV	NOMSIM,R2
	BIS	#77,R2
	INC	R2
	SUB	R2,R3

	MOV	#5,R2		;  декемхе мю 32
1$:	ASR	R3
	SOB	R2,1$

	MOV	R3,R2		;  слмнфемхе мю 5
	ASL	R3
	ASL	R3
	ADD	R3,R2

	RTS	PC



;   ====================================
;   *** 4. акнй сопюбкъчыху яхлбнкнб ***
;   ====================================


BUS:	CMPB	R0,#12		;  оя
	BNE	1$
	JSR	PC,PFUS
	BIT	#77,NOMSIM
	BNE	3$
	TSTB	PRISU
	BNE	KBUS
3$:	BIS	#77,NOMSIM
	INC	NOMSIM
	JSR	PC,PREDSK
	BR	KBUS

1$:	TSTB	PRBLR
	BNE	PFUS

	CMPB	R0,#14		;  яап
	BNE	2$
	JSR	PC,PSBR
	BR	KBUS

2$:	CMPB	R0,#7		;  гб
	BNE	PFUS
	JSR	PC,PSIGN
	BR	KBUS


;    --------------------------------------------
;    ** 4.1. тнплхпнбюмхе сопюбкъчыецн яхлбнкю **
;    --------------------------------------------

PFUS:	TSTB	PRISU		;  пефхл хмд.яс.
	BEQ	KFUS

PFUS1:	SUB	#20,R0		;  пед. яхлбнкш
	BMI	2$
4$:	JSR	PC,BGS
	BR	KFUS

2$:	CMPB	R0,#-10		;  бь
	BNE	3$
	MOV	#1,R0
	BR	4$

3$:	ADD	#100,R0		;  яс
	COMB	PRINWS
	JSR	PC,BGS
	COMB	PRINWS
	BR	KFUS

KFUS:
KBUS:	RTS	PC



;   =======================================
;   *** 5. акнй тсмйжхнмюкэмшу яхлбнкнб ***
;   =======================================


BFS:	CMPB	R0,#202		;  хяс
	BNE	1$
	COMB	PRISU
	MOV	#3,R0
	BR	11$

1$:	CMPB	R0,#204		;  акп
	BNE	2$
	COMB	PRBLR
	MOV	#4,R0
11$:	BR	BFS1

2$:	CMPB	R0,#214		;  по
	BNE	3$
	JSR	PC,PPRP
	BR	KBFS

3$:	MOVB	PRGRAF,R5
	BNE	BFS0
	TSTB	PRBLR
	BNE	BFS2

BFS0:	SUB	#221,R0		;  бшанп йндю
	BMI	KBFS
	ASL	R0
	ADD	R0,PC
	BR	21$
	BR	21$
	BR	21$
	BR	21$
	BR	25$
	BR	26$
	BR	27$
	BR	KBFS
	BR	31$
	BR	32$
	BR	33$
	BR	34$
	BR	35$
	BR	36$
	BR	37$

21$:	TSTB	PRCW
	BEQ	KBFS
	MOV	KMASCW(R0),R0	;  й/г/я/в
	CLRB	PRINWF
	JSR	PC,PSTK
	MOV	R0,MASCW
	JSR	PC,PFK
	BR	KBFS

25$:	JSR	PC,PPGRAF	;  цпют
1$:	MOV	#5,R0
	BR	BFS1

26$:	TST	R5		;  гюо
	BEQ	KBFS
	CLRB	PRSTIR
	COMB	PRZAP
	BR	1$

27$:	TST	R5		;  ярхп
	BEQ	KBFS
	CLRB	PRZAP
	COMB	PRSTIR
	BR	1$

31$:	TST	R5		;  яап. й. ярп.
	BNE	KBFS
	JSR	PC,PSBKST
	BR	KBFS

32$:	JSR	PC,PSTK		;  цюь. йспя.
	COMB	PRGAHK
	JSR	PC,PFK
	BR	KBFS

33$:	JSR	PC,PPCW		;  жб
	BR	KBFS

34$:	COMB	PRINWS		;  хмб.я.
	MOV	#2,R0
	BR	BFS1

35$:	JSR	PC,PINWF	;  хмб. щ.
	BR	KBFS

36$:	MOV	FON,FONSS	;  сяр.хмд.
	MOV	MASCW,MCWSS
	MOV	PRPODC,PRPSSS
	MOVB	PRCW,PRCWSS
	JSR	PC,PSTK
	JSR	PC,PFSSTR
	JSR	PC,PFK
	BR	KBFS

37$:	COMB	PRPODC		;  ондв.
	MOV	#1,R0

BFS1:	JSR	PC,PFINDR	;  тнпл. хмд. пефхлю
	BR	KBFS

BFS2:	JSR	PC,PFFS		;  тнпл. тсмйж. яхлб.

KBFS:	RTS	PC


;    ------------------------------------------
;    ** 5.1. оепейкчвемхе пюяьхпеммни оюлърх **
;    ------------------------------------------

PPRP:	COMB	PRRP
	BNE	PPRP2

PPRP1:	MOV	#40000,ANVP	;  ме пюяьхпеммюъ оюлърэ
	MOV	#40000,DVPB
	MOV	#36000,DGPB
	MOV	#3000,KSVP
	MOV	#NZRUL0,@#ARRUL
	BR	KPRP

PPRP2:	MOV	#70000,ANVP	;  пюяьхпеммюъ оюлърэ
	MOV	#10000,DVPB
	MOV	#5000,DGPB
	MOV	#400,KSVP
	MOV	#NZRUL1,@#ARRUL
	MOV	#77000,R3	;- сяр. хмд. по.
	MOV	#340,R4
1$:	MOV	FONSS,(R3)+
	SOB	R4,1$
	MOV	MCWSS,R1
	JSR	PC,PZTVSN

KPRP:	MOV	#DSLSTB,BAZVP	;  сяр. аюгш
	JSR	PC,PSBR		;  яапня щйпюмю
	JSR	PC,PFSSTR	;  тнпл. як. ярп.

	RTS	PC


;    -------------------------------------------
;    ** 5.2. оепейкчвемхе цпютхвеяйнцн пефхлю **
;    -------------------------------------------

PPGRAF:	JSR	PC,PSTK
	COMB	PRGRAF
	BEQ	1$

	MOVB	NMPGT,MASPGT	;  сяр. цпют. пефхлю
	CLR	DGW
	CLR	SCTVS
	MOV	AS,R0
	MOV	R0,AGT
	SUB	ANVP,R0
	SUB	BAZVP,R0
	BPL	2$
	ADD	DVPB,R0
2$:	MOV	R0,AGTVP
	BR	KPGRAF

1$:	CLRB	PRZAP		;  яапня цпют. пефхлю
	CLRB	PRSTIR
	TSTB	PRCW		;- бшвхякемхе AS
	BEQ	3$
	BIC	#1,NOMSIM
3$:	CLR	R5
	JSR	PC,PFASIM

KPGRAF:	MOV	#5,R0
	JSR	PC,PFINDR
	JSR	PC,PFK

	RTS	PC


;    -----------------------------
;    ** 5.3. яапня йнмжю ярпнйх **
;    -----------------------------

PSBKST:	MOV	AS,-(SP)

1$:	JSR	PC,PSBRS	;  яапня
	ADD	DSIMB,AS
	BIT	#77,AS
	BNE	1$

	MOV	(SP)+,AS	;  тнплхпнбюмхе йспянпю
	JSR	PC,PFSK

	RTS	PC


;    ---------------------------------------
;    ** 5.4. оепейкчвемхе жбермнцн пефхлю **
;    ---------------------------------------

PPCW:	JSR	PC,PSTK

	COMB	PRCW
	BNE	1$

	DEC	DSIMB		;  вепмн-аекши
	CLR	R5
	TSTB	PRINWF
	BEQ	2$
	COM	R5
2$:	MOV	R5,FON
	COM	R5
	MOV	R5,MASCW
	MOVB	#1,NMPGT	;- цпют.
	BICB	#252,MASPGT
	BR	3$

1$:	INC	DSIMB		;  жбермни
	TSTB	PRGRAF
	BNE	6$
	INC	NOMSIM
	BIC	#1,NOMSIM
	CLR	R5
	JSR	PC,PFASIM
6$:	MOVB	#3,NMPGT	;- цпют.
	MOVB	MASPGT,R5
	BITB	#252,R5
	BEQ	4$
	ASRB	R5
	BR	5$
4$:	ASLB	R5
5$:	BISB	R5,MASPGT

3$:	JSR	PC,PFINDT
	JSR	PC,PFK

	RTS	PC


;    ------------------------
;    ** 5.5. хмбепяхъ тнмю **
;    ------------------------

PINWF:	COMB	PRINWF

	MOV	MASCW,R0	;  ялемю люянй
	MOV	FON,MASCW
	MOV	R0,FON

	MOV	MASCW,R5	;  ондцнр. оюпюл
	XOR	R5,R0
	MOV	BAZVP,R1
	ADD	ANVP,R1
	MOV	DGPB,R2
	ASR	R2

2$:	XOR	R0,(R1)+	;  хмбепяхъ тнмю
	TST	R1
	BPL	1$
	SUB	DVPB,R1
1$:	SOB	R2,2$

	RTS	PC


;    -----------------------------------------------
;    ** 5.6. тнплхпнбюмхе тсмйжхнмюкэмшу яхлбнкнб **
;    -----------------------------------------------

PFFS:	CMPB	R0,#225		;- ц
	BNE	1$
	MOV	#267,R0
	BR	2$
1$:	CMPB	R0,#226		;- г
	BNE	3$
	MOV	#312,R0
	BR	2$
3$:	CMPB	R0,#227		;- C
	BNE	4$
	MOV	#303,R0
	BR	2$

4$:	ADD	#20,R0

2$:	COMB	PRINWS		;  тнпл. яхлб.
	JSR	PC,PFSIM
	COMB	PRINWS

	ADD	DSIMB,NOMSIM	;  хгл. юдпеяю
	JSR	PC,PFASIM
	JSR	PC,PFSK

	RTS	PC



;   =================================
;   *** 6. акнй рейярнбни цпютхйх ***
;   =================================


BTG:	CMPB	R0,#37		;  сйюгюрекх мюопюбкемхъ
	BHI	1$
	JSR	PC,PFNGW
	BR	KBTG1

1$:	BIC	#177400,R0
	CMPB	R0,#71		;  жхтпш
	BHI	2$
	JSR	PC,PFDGW
	BR	KBTG

2$:	CMPB	R0,#237		;  тсмйжхнмюкэмше йндш
	BHI	KBTG1
	JSR	PC,BFS

KBTG1:	CLR	DGW
KBTG:	RTS	PC


;    --------------------------------------
;    ** 6.1. тнплхпнбюмхе мюопюбкемхъ GW **
;    --------------------------------------

PFNGW:	JSR	PC,PSTGK

	CLR	R1		;  ондц. оюпюл.
	CLR	R2
	MOV	#DTVSTB,R4

	MOVB	R0,R3		;  бшанп йндю
	SUB	#31,R3
	BMI	1$
	ASL	R3
	ADD	R3,PC
	BR	31$
	BR	32$
	BR	33$
	BR	34$
	BR	35$
	BR	36$
	BR	37$

31$:	INC	R1		;  йо
	BR	PFNGW2

32$:	SUB	R4,R2		;  йб
	BR	PFNGW2

33$:	ADD	R4,R2		;  йм
	BR	PFNGW2

34$:	SUB	R4,R2		;  йкб
	DEC	R1
	BR	PFNGW2

35$:	SUB	R4,R2		;  йоб
	INC	R1
	BR	PFNGW2

36$:	ADD	R4,R2		;  йом
	INC	R1
	BR	PFNGW2

37$:	ADD	R4,R2		;  йкм
	DEC	R1
	BR	PFNGW2

1$:	CMPB	R0,#10		;  йк
	BNE	2$
	DEC	R1
	BR	PFNGW2

2$:	CMPB	R0,#22		;  ймщ
	BNE	3$
	CLR	NOMSIM
	JMP	PSBR1

3$:	CMPB	R0,#14		;  яапня
	BNE	KFNGW
	JSR	PC,PSBR
	RTS	PC

PFNGW2:	MOV	DGW,R4		;  гюцпсгйю DGW
	BNE	PFGW
	CLRB	R0
	SWAB	R0
	INC	R0
	MOV	R0,R4


;    ----------------------------------------------
;    ** 6.1.1. тнплхпнбюмхе цпютхвеяйнцн бейрнпю **
;    ----------------------------------------------

PFGW:	TSTB	PRZAP		;  гюо
	BEQ	1$
	MOV	MASCW,R0
	BR	2$

1$:	TSTB	PRSTIR		;  ярхп
	BEQ	3$
	MOV	FON,R0

2$:	MOVB	MASPGT,R5	;  тнплхп. рнвйх
	BICB	R5,@AGT
	MOV	R0,R3
	COM	R5
	BIC	R5,R3
	BISB	R3,@AGT
	JSR	PC,PFAGT
	SOB	R4,2$
	BR	KFGW

3$:	JSR	PC,PFAGT	;  оепелеыемхе
	SOB	R4,3$

KFGW:
KFNGW:	JSR	PC,PFGK
	RTS	PC


;    -------------------------------
;    ** 6.1.1.1. тнплхпнбюмхе AGT **
;    -------------------------------

PFAGT:	MOV	AGTVP,R5
	ADD	R2,R5
	ADD	R2,SCTVS
	MOV	MASPGT,-(SP)
	MOV	#100,R3

	TST	R1		;  опълн/бкебн/бопюбн
	BEQ	1$
	BMI	2$

	ASLB	(SP)		;  ядбхц бопюбн
	BCC	3$
	ADCB	(SP)
	INC	R5
	INC	NOMSIM
	BIT	#77,R5
	BNE	3$
	SUB	R3,R5
	SUB	R3,NOMSIM
3$:	TSTB	PRCW
	BEQ	1$
	ASLB	(SP)
	ADCB	(SP)
	BR	1$

2$:	CLC
	RORB	(SP)		;  ядбхц бкебн
	BCC	4$
	BISB	#200,(SP)
	BIT	#77,R5
	BNE	21$
	ADD	R3,R5
	ADD	R3,NOMSIM
21$:	DEC	NOMSIM
	DEC	R5
4$:	TSTB	PRCW
	BEQ	1$
	CLC
	RORB	(SP)
	BCC	1$
	BISB	#200,(SP)

1$:	MOV	(SP)+,MASPGT
	MOV	#DSSTRB,-(SP)
	TST	SCTVS
	BPL	11$
	NEG	R3
	NEG	(SP)
	BR	12$
11$:	CMP	SCTVS,(SP)
	BLO	13$
12$:	SUB	(SP),SCTVS
	ADD	R3,NOMSIM
	BMI	14$
	CMP	NOMSIM,KSVP
	BLO	13$
14$:	SUB	(SP),R5
	MOV	R5,-(SP)	;- пскнм
	JSR	R4,PSREG
	CLR	R5
	JSR	PC,PFASIM
	JSR	R4,PWREG
	MOV	(SP)+,R5
13$:	MOV	(SP)+,R3

PFAGT1:	MOV	R5,AGTVP	;- AGTVP
	ADD	BAZVP,R5	;- AGT
	BIC	#140000,R5
	ADD	ANVP,R5
	MOV	R5,AGT

	RTS	PC


;    ----------------------------------------------
;    ** 6.1.2. тнплхпнбюмхе цпютхвеяйнцн йспянпю **
;    ---------------------------------------------

PFGK:				;  тнплхпнбюмхе х ярхпюмхе йспянпю
PSTGK:	TSTB	PRGAHK		;  опнб. опхгмюйю
	BNE	KFGK

	MOV	MASCW,R4	;  тнплхпнбюмхе люяйх рнвйх
	MOV	FON,R5
	XOR	R5,R4
	CLR	R5
	BISB	MASPGT,R5
	COM	R5
	BIC	R5,R4

	MOV	AGT,R3		;  ондц. юдпеянб
	ASR	R3
	BCC	1$
	SWAB	R4
1$:	ASL	R3
	MOV	R3,R1
	SUB	#300,R3
	CMP	R3,ANVP
	BHIS	11$
	ADD	DVPB,R3

11$:	MOV	#7,R2		;  тнплхпнбюмхе бепр. вепрш
2$:	XOR	R4,(R3)
	JSR	PC,PFTAN
	SOB	R2,2$

	MOV	#3,R2		;  ядбхц люяйх бкебн
	CLC
	MOVB	PRCW,R5
	BEQ	3$
	ASL	R2
3$:	ROR	R4		;- ядбхц
	BCC	4$
	DEC	R1
	ROR	R4
4$:	SOB	R2,3$
	BIC	#1,R1
	CMP	R1,ANVP
	BHI	41$
	ADD	DVPB,R1

41$:	MOV	#7,R2		;  тнплхпнбюмхе цнпхг.вепрш
7$:	XOR	R4,(R1)
	ROL	R4
	BCC	5$
	JSR	PC,PFGK1
5$:	MOV	R5,R5
	BEQ	6$
	ROL	R4
	BCC	6$
	JSR	PC,PFGK1
6$:	SOB	R2,7$
KFGK:	RTS	PC

PFGK1:	ADD	#2,R1		;  опхпюыемхе юдпеяю
	BPL	1$
	SUB	DVPB,R1
1$:	SEC
	ROL	R4
	RTS	PC


;    --------------------------------
;    ** 6.2. тнплхпнбюмхе дкхмш GW **
;    --------------------------------

PFDGW:	SUB	#60,R0
	BPL	1$
	CLR	R0
	BR	2$

1$:	MOV	DGW,R1		;  слмнфемхе мю 10
	ASL	R1
	ADD	R1,R0
	ASL	R1
	ASL	R1
	ADD	R1,R0
2$:	MOV	R0,DGW

KFDGW:	RTS	PC
