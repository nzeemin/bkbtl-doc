.TITLE	EXECUTION MINIMAL
.ENABLE	LC

.IF EQ,MASINA-BK0011
.MACRO	.BJSR0
	EMT	54
.ENDM
.MACRO	.BJMP
	EMT	56
.ENDM
.ENDC

.MACRO	.BJSR	ADR
.IF EQ,MASINA-BK0011
	MOV	#PAG1,-(SP)
	MOV	#ADR,-(SP)
	.BJSR0
.IFF
	CALL	ADR
.ENDC
.ENDM

.GLOBL	BRKO,CLOALL,FNDBUF,FILINP,ISV
.GLOBL	TXEND,LYGIS,STRREG,STRSIZ,CURON,CUROFF,STACK
.GLOBL	TRFLAG,TRIN,LIMIT,SAVJMP
.GLOBL	ADRHLT,STAR,ENDCOD,LENT,CIKL,IVEIL
.GLOBL	ISVTO,ISVETO,FCB,EGO,ISGUNP,HIMEM
.GLOBL	NAMEW,NAMER,RD.,WR.,FR.,ADRR,ADRW,LENW,FER.
.GLOBL	BUF,TABTOP,BUFOUT,FREBEG,FRELEN,NUMBER
.GLOBL	DATINP,DATPTR,IODEV,MAG				;MAXFIL
.GLOBL	WRITE,COPOUT,CISLO,TSTFRE
.GLOBL	EILUTE,LRUN,CLRTX1,ASCCOD,TEXT,ERRS,GRBCOL
.GLOBL	CLRBUF,CLRCOD,LPTPOS,PFACT			;SIGNAL
.GLOBL	VCTNMB						;ONERFL

.IF EQ,MASINA-BK0010
.GLOBL	MSTART,FNKSTR,ASPORT
.ENDC

.IF EQ,MASINA-BK0011
.GLOBL	PNTSAV,PAG1,ENDST,HALTST,T113
.GLOBL	ID.,ID$,STMT$,ASCCD0,ERRBYT
.GLOBL	VIRSCR,VIREND,STARP,ISVP
.IFF
.GLOBL	ENDPRG
.ENDC

.IF DF,SNG
.GLOBL	ADR,RI,IR
.IFF
.GLOBL	$DADD$,ID,SI,IS,SD,DCMP,DGO
.ENDC

.IF DF,HOOKS
	.GLOBL	HPUTCH,HPUTLN,HLOCAT,HLOOK,HLPOS
.ENDC

.IIF EQ,MASINA-DISK	.GLOBL	RT11,SYSPTR
.IIF EQ,MASINA-NET	.GLOBL	IRPS
.IIF EQ,MASINA-CASET	.GLOBL	MAGNIT
.IIF EQ,MASINA-DVK3	.GLOBL	CLSCR

.IF GE,MASINA-DVK2B
.GLOBL	TABX,TABY,INBF0
.GLOBL	NIM,LN,LL,EBUF,CURS,LOK,CRTGRP,GRPCLS
.GLOBL	SETWDT,MIDLE
.IFTF

.IF EQ,MASINA-BK0011

ENDPRG::CALL	CLOALL
	MOV	#PAG1,-(SP)
	MOV	#ENDST,-(SP)
	.BJMP
HALT::	MOV	#PAG1,-(SP)
	MOV	#HALTST,-(SP)
	.BJMP
.ENDC
;---------------------------------------------
ICMP::	SUB	(SP)+,(SP)+
	JMP	@(R4)+
;
NEXCMP::MOV	CIKL,R1
	MOV	(R1)+,R0
	MOV	SP,R2
	MOV	(R2)+,(R0)+
.IF DF,SNG
.IFF
	BIT	#20000,-4(R0)
	BEQ	NDCMP
	MOV	(R2)+,(R0)
	MOV	@R2,R2
	ROL	R2
	ADC	@R0
	ADC	-(R0)
	BVS	1$
	BCC	NCMP
1$:	TRAP	6
;
NDCMP:	MOV	(R2)+,(R0)+
	MOV	(R2)+,(R0)+
.IFTF
	MOV	(R2),(R0)+
NCMP:	TST	(R1)+
	MOV	(R1)+,-(SP)
	MOV	(R1)+,-(SP)
.IFF
	MOV	(R1)+,-(SP)
	MOV	@R1,-(SP)
	JMP	DCMP
;
.IFTF
SCMP::	MOV	@PC,R0
	MOV	4(SP),R1
	BGE	1$
	ASL	R0
	MOV	(SP)+,R2
	BLT	2$
	BR	4$
1$:	MOV	(SP)+,R2
	BLT	5$
2$:	CMP	R1,R2
	BNE	3$
	CMP	4(SP),@SP
	BNE	3$
	CLR	R0
3$:	ROR	R0
	BCS	5$
4$:	NEG	R0
5$:	ADD	#6,SP
	TST	R0
	JMP	@(R4)+
;
$CMP::	MOV	(SP)+,R2
	MOV	(SP)+,R3
	MOV	(SP)+,R1
	MOV	(SP)+,R0
	SUB	R1,R2
	BPL	1$
	ADD	R2,R1
1$:	DEC	R1
	BMI	2$
	CMPB	(R0)+,(R3)+
	BEQ	1$
	JMP	@(R4)+
2$:	NEG	R2
	JMP	@(R4)+
;
SUM2::	.WORD	0
;
EQ::	BEQ	TRUE
FALSE::	CLR	-(SP)
	JMP	@(R4)+
;
NE::	BNE	TRUE
	BR	FALSE
;
LE::	BLE	TRUE
	BR	FALSE
;
GE::	BGE	TRUE
	BR	FALSE
;
LT::	BLT	TRUE
	BR	FALSE
;
GT::	BLE	FALSE
TRUE::	MOV	#-1,-(SP)
	JMP	@(R4)+
;----------------------------------------
IFT::	TST	(SP)+
	BNE	JMP
	BR	NJMP
;
IFF::	TST	(SP)+
	BEQ	JMP
	BR	NJMP
;
NEXJMP::MOV	CIKL,R1		;---STOP---
.IFT
	TST	12(R1)
.IFF
	TST	22(R1)
.IFTF
	BMI	2$
	TST	R0
	BGT	3$
1$:	MOV	2(R1),R4
	JMP	@(R4)+		;-------+22
2$:	TST	R0
	BGE	1$
.IFT
3$:	ADD	#14,CIKL
.IFF
3$:	ADD	#24,CIKL
.IFTF
	JMP	@(R4)+
;
JSR::	MOV	CIKL,R0
	TST	-(R0)
	CMP	R0,ENDCOD
	BLOS	STCKER
	MOV	LIMIT,@R0
	MOV	R4,-(R0)
	MOV	R0,LIMIT
SUB:	MOV	R0,CIKL
;
JMP::	MOV	@R4,R4		;---STOP---
	JMP	@(R4)+		;--------+2
;
RTSTO::	MOV	LIMIT,R0
	CMP	R0,LENT
	BHIS	RETERR
	TST	(R0)+
	MOV	(R0)+,LIMIT
	BR	SUB
RETERR:	TRAP	3
;
RTS::	MOV	LIMIT,R0	;---STOP---
	CMP	R0,LENT
	BHIS	RETERR
	MOV	(R0)+,R4
	MOV	(R0)+,LIMIT	;-------+14
	MOV	R0,CIKL
;
NJMP:	TST	(R4)+
	JMP	@(R4)+		;-------+26
;
ONJMP::	MOV	(SP)+,R0
	BEQ	ONSKIP
	BIT	#177400,R0
	BNE	ONERR
	CMP	R0,@R4
	BLE	JUMP
ONSKIP:	MOV	(R4)+,R0
	ASL	R0
	ADD	R0,R4
	JMP	@(R4)+
ONERR:	TRAP	5
;
ONJSR::	MOV	(SP)+,R0
	BEQ	ONSKIP
	BIT	#177400,R0
	BNE	ONERR
	CMP	R0,@R4
	BGT	ONSKIP
	MOV	@R4,R1
	ASL	R1
	ADD	R4,R1
	MOV	CIKL,R2
	TST	-(R2)
	CMP	R2,ENDCOD
	BLOS	STCKER
	MOV	LIMIT,@R2
	MOV	R1,-(R2)
	MOV	R2,CIKL
	MOV	R2,LIMIT
JUMP:	ASL	R0
	ADD	R0,R4
	BR	JMP
STCKER:	TRAP	7
;
.IFF
DFNRET::MOV	#4,R1
	BR	RETFN
;
.IFTF
SFNRET::MOV	#2,R1
	BR	RETFN
;
IFNRET::MOV	#1,R1
;
RETFN:	MOV	PFACT,R0
	MOV	(R0)+,PFACT
	MOV	R1,R2
	ASL	R2
	ADD	SP,R2
1$:	MOV	-(2),-(0)
	SOB	R1,1$
	MOV	R0,SP
	MOV	@(4)+,R4
	JMP	@(4)+
;
DFN::	MOV	(4)+,R0
	MOV	(4)+,R1
	MOV	R4,@R1
	MOV	R0,R4
	JMP	@(4)+
;
FNST::	MOV	PFACT,-(SP)
	MOV	SP,PFACT+2
	JMP	@(4)+
;
FNCALL::MOV	(4)+,R0
	MOV	(0)+,R2
	BEQ	2$
	MOV	@R4,R1
	INC	R1
1$:	CMP	(4)+,(2)+
	BNE	3$
	SOB	R1,1$
	MOV	R4,@R0
	MOV	R2,R4
	MOV	PFACT+2,PFACT
	JMP	@(4)+
2$:	TRAP	18.
3$:	TRAP	5
;-----------------------------------------
APAR::	MOV	PFACT,-(SP)
	SUB	(4)+,@SP
	JMP	@(4)+
;
.IFF
DPAR::	MOV	#4,R1
	BR	PARAM
;
.IFTF
SPAR::	MOV	#2,R1
	BR	PARAM
;
IPAR::	MOV	#1,R1
;
PARAM:	MOV	PFACT,R0
	SUB	(4)+,R0
1$:	MOV	-(0),-(SP)
	SOB	R1,1$
	JMP	@(4)+
;
.IFF
DCONST::MOV	(R4)+,-(SP)
	MOV	(R4)+,-(SP)
.IFTF
SCONST::MOV	(R4)+,-(SP)
ICONST::MOV	(R4)+,-(SP)
	JMP	@(R4)+
;
.IFF
DXVAR::	MOV	(SP)+,R1
	BR	DVAR1
;
DVAR::	MOV	(R4)+,R1
DVAR1:	ADD	#10,R1
	MOV	-(R1),-(SP)
	MOV	-(R1),-(SP)
	MOV	-(R1),-(SP)
	MOV	-(R1),-(SP)
	JMP	@(R4)+
;
.IFTF
SXVAR::	MOV	(SP)+,R1
	BR	SVAR1
;
SVAR::	MOV	(R4)+,R1
SVAR1:	MOV	2(R1),-(SP)
	MOV	@R1,-(SP)
	JMP	@(R4)+
;
IXVAR::	MOV	@(SP)+,-(SP)
	JMP	@(R4)+
;
IVAR::	MOV	@(R4)+,-(SP)
	JMP	@(R4)+
;
$XVAR::	MOV	@SP,R0
	MOVB	(R0)+,R1
	MOVB	(R0)+,@SP
	MOVB	(R0)+,1(SP)
	CLR	-(SP)
	BISB	R1,@SP
	JMP	@(R4)+
;
$XMOV::	MOV	(SP)+,R0
	MOV	(SP)+,R1
	MOV	(SP)+,R2
	MOVB	R0,(R2)+
	MOVB	R1,(R2)+
	SWAB	R1
	MOVB	R1,(R2)+
	JMP	@(R4)+
;
SXMOV::	MOV	4(SP),R1
.IFF
	BR	XMOV
;
DXMOV::	MOV	10(SP),R1
	MOV	(SP)+,(R1)+
	MOV	(SP)+,(R1)+
.IFTF
XMOV::	MOV	(SP)+,(R1)+
	MOV	(SP)+,@R1
SPJ:	TST	(SP)+
	JMP	@(R4)+
;
;
IXMOV::	MOV	(SP)+,@(SP)+
	JMP	@(R4)+
;
SMOV::	MOV	(R4)+,R1
.IFF
	BR	MOVS
;
DMOV::	MOV	(R4)+,R1
	MOV	(SP)+,(R1)+
	MOV	(SP)+,(R1)+
.IFTF
MOVS:	MOV	(SP)+,(R1)+
	MOV	(SP)+,@R1
	JMP	@(R4)+
;
IMOV::	MOV	(SP)+,@(R4)+
	JMP	@(R4)+
;
MIDMOV::MOV	(SP)+,R3
	MOV	(SP)+,R2
	MOV	(SP)+,R1
	MOV	(SP)+,R0
	CMP	R3,R1
	BLE	1$
	MOV	R1,R3
1$:	DEC	R3
	BMI	3$
	MOVB	(R2)+,(R0)+
	BR	1$
3$:	JMP	@(R4)+
;----------------------------------------------
.IFF
FORDBL::
.IFTF
FORSNG::MOV	CIKL,R0
	MOV	(SP)+,-(R0)
	MOV	(SP)+,-(R0)
.IFF
	MOV	(SP)+,-(R0)
	MOV	(SP)+,-(R0)
	MOV	(SP)+,-(R0)
	MOV	(SP)+,-(R0)
.IFTF
	BR	FORMOV
;
FORINT::MOV	CIKL,R0
FORMOV:	MOV	(SP)+,-(R0)
	MOV	(SP)+,-(R0)
	MOV	(R4)+,-(R0)
	MOV	(R4)+,-(R0)
	CMP	R0,ENDCOD
	BLO	ERR7
	MOV	R0,CIKL
JEN3:	JMP	@(R4)+
ERR7:	TRAP	7
;
LIT::	MOV	(SP)+,R2
	BEQ	SPJ
	BR	OUT2
;
NXSKIP::MOV	CIKL,R1
	MOV	(R4)+,R0
	BNE	1$
	MOV	@R1,R0
1$:	CMP	R1,LIMIT
	BHIS	3$
	CMP	R0,(R1)+
	BEQ	JEN3
	MOV	-(R1),R2
	ADD	#10,R1
.IFT
	TST	-(R2)
	BMI	10$
	CMP	(1)+,(1)+
.IFF
	BIT	#40000,-(R2)
	BNE	10$
	ADD	#14,R1
.IFTF
10$:	MOV	R1,CIKL
	BR	1$
3$:	TRAP	1
;
NEXSNG::
.IFT
	TST	-2(0)
	BMI	1$
	MOV	2(0),-(SP)
	MOV	@R0,-(SP)
	ADD	#6,R1
	MOV	(1)+,-(SP)
	MOV	(1)+,-(SP)
	JMP	ADR
.IFF
	MOV	-2(R0),R2
	ROL	R2
	BMI	1$
	ROL	R2
	BPL	NEXDBL
	CLR	-(SP)
	CLR	-(SP)
	CMP	(R0)+,(R0)+
	BR	NEXADD
.IFTF
1$:	CMP	(R4)+,(R4)+
;
NEXINT::CMP	(R1)+,(R1)+	;---STOP---
	MOV	@R1,R2
	ADD	@R0,R2
	BVS	4$
	MOV	R2,@R0
	TST	@R1
	BMI	2$
	CMP	R2,-(R1)
	BGT	3$
1$:	MOV	-(R1),R4
	JMP	@(R4)+		;-------+24
2$:	CMP	R2,-(R1)
	BGE	1$
3$:	ADD	#10,CIKL
	JMP	@(R4)+
4$:	TRAP	6
;
.IFF
NEXDBL::ADD	#6,R0
	MOV	@R0,-(SP)
	MOV	-(R0),-(SP)
NEXADD:	MOV	-(R0),-(SP)
	MOV	-(R0),-(SP)
	ADD	#12,R1
	MOV	(R1)+,-(SP)
	MOV	(R1)+,-(SP)
	MOV	(R1)+,-(SP)
	MOV	@R1,-(SP)
	JMP	$DADD$
;------------------------------------------
.IFTF
NEWLIN::MOV	#12,R0
	BR	JEND
;
JCLS::
.IF EQ,MASINA-UK
	TST	CRTGRP
	BEQ	1$
	JMP	GRPCLS
1$:
.ENDC

.IF EQ,MASINA-DVK3
	TST	@#CRTGRP
	BPL	1$
	CALL	CLSCR
1$:
	BIT	#40000,@#CRTGRP
	BEQ	2$
	JMP	@(R4)+
2$:
.ENDC
	MOV	#14,R0
JEND:	MOV	R4,SAVJMP
	.BJSR	ISVTO
JEN2:	CLR	SAVJMP
	JMP	@(R4)+
;
SOUTSP::CALL	EGO
.IFF
	BR	OUT
;
DOUTSP::CALL	DGO
.IFTF
OUT:	MOV	(SP)+,R2
OUT2:	MOV	(SP)+,R1
	BR	OUT1
;
IOUTSP::MOV	(SP)+,R5
	.BJSR	ISGUNP
OUT1:	MOV	R4,SAVJMP
	.BJSR	ISVETO
	BR	JEN2
;
.ENDC
;
ZONA::
	MOV	@#IODEV,R3
	BLT	3$
	BGT	4$
.IFF
	CALL	LOOKXY
	MOV	R1,-(SP)
.IFT
	MOV	@#TABX,-(SP)
.IFTF
	BR	41$
3$:

.IIF DF,HOOKS	CALL	HLPOS

	MOV	@#LPTPOS,-(SP)
	BR	41$
4$:	CLR	-(SP)
41$:	SUB	#20,@SP
	BGT	41$
	BEQ	42$
	NEG	@SP
	BR	SPC1
42$:	MOV	#20,@SP
	BR	SPC1
;
SPC::	CALL	TIKR
	BR	SPC1
;
TAB::	CALL	TIKR
	MOV	@#IODEV,R3
	BEQ	1$
	BGT	SPC1

.IIF DF,HOOKS	CALL	HLPOS

	MOV	@#LPTPOS,R1
	BR	3$

.IFF
1$:	CALL	LOOKXY
.IFT
1$:	MOV	@#TABX,R1
.IFTF
3$:	SUB	R1,@SP
SPC1:	MOV	#40,R0
1$:	DEC	@SP
	BLT	2$
	.BJSR	ISVTO
	BR	1$
2$:	TST	(SP)+
	JMP	@(4)+
;
TIKR:	TSTB	3(SP)
	BNE	BADARG
	RETURN
BADARG:	TRAP	5
.IFF
;
AT::	CALL	TIKR
	MOV	(SP)+,R2
	TST	@#IODEV
	BNE	TAB
	CALL	TIKR
.IF EQ,MASINA-BK0011
	MOV	(SP)+,R0
	SWAB	R2
	BIS	R2,R0
.IFF
	MOV	(SP)+,R1
.ENDC

.IIF DF,HOOKS	CALL	HLOCAT

	EMT	24
	JMP	@(R4)+
;
.IF NDF,SNG
DLOCX::	CMP	(SP)+,(SP)+
.IFTF
SLOCX::	TST	(SP)+
ILOCX::	TST	(SP)+
LOCX::	CALL	LOOKXY
	MOV	R1,-(SP)
	JMP	@(R4)+
;
.IFT
DLOCY::	CMP	(SP)+,(SP)+
.ENDC
SLOCY::	TST	(SP)+
ILOCY::	TST	(SP)+
LOCY::	CALL	LOOKXY
	MOV	R2,-(SP)
	JMP	@(R4)+
;
LOOKXY::
.IF DF,HOOKS
	CALL	HLOOK
.ENDC
.IF EQ,MASINA-BK0011
	MOV	R0,-(SP)
	EMT	26
	MOVB	R0,R1
	CLRB	R0
	SWAB	R0
	MOV	R0,R2
	MOV	(SP)+,R0
.IFF
	EMT	26
.ENDC
	RETURN
;
.IFT
AT::	CALL	TIKR
	MOV	(SP)+,R2
	TST	IODEV
	BNE	TAB
	CALL	TIKR
.IF DF,HOOKS
	CALL	HLOCAT
.ENDC
	CMP	R2,#LN
	BGT	BADARG
	CMP	@SP,LL
	BGE	BADARG
	MOV	#EBUF,R1
	ADD	@SP,R1
	MOV	(SP)+,TABX
	MOV	R2,TABY
	BEQ	4$
3$:	ADD	LL,R1
	SOB	R2,3$
4$:	MOV	R1,CURS
	CALL	LOK
	JMP	@(R4)+
;
.IF NDF,SNG
DLOCX::	CMP	(SP)+,(SP)+
.IFTF
SLOCX::	TST	(SP)+
ILOCX::	TST	(SP)+
 LOCX::
.IF DF,HOOKS
	CALL	HLOOK
.ENDC
	MOV	TABX,-(SP)
	JMP	@(4)+
;
.IFT
DLOCY::	CMP	(SP)+,(SP)+
.ENDC
SLOCY::	TST	(SP)+
ILOCY::	TST	(SP)+
 LOCY::
.IF DF,HOOKS
	CALL	HLOOK
.ENDC
	MOV	TABY,-(SP)
	JMP	@(4)+
;
.IFTF
LOC::	TST	(SP)+
	BNE	1$
	.BJSR	CUROFF
	JMP	@(R4)+
1$:	.BJSR	CURON
	JMP	@(R4)+
;-----------------------------------------------
GETLIT::MOV	R4,SAVJMP
	SUB	#12,SAVJMP
	BR	G01
;
RESTV::	MOV	(4)+,DATPTR
	JMP	@(4)+
;
READV::	MOV	DATPTR,R3
	BNE	1$
	TRAP	4
1$:	MOV	#-1,DATINP
	MOV	R4,SAVJMP
	MOV	(4)+,-(SP)
	BR	G1
;
GET::	MOV	R4,SAVJMP
	SUB	#2,SAVJMP
G01:	MOV	(R4)+,-(SP)
	MOV	IODEV,DATINP
	BNE	G03
	MOV	#'?,R0
	BR	G00
G13:	TSTB	IODEV+1
	BNE	1$
	CMPB	@R3,#12
	BEQ	G02
1$:	CMP	@R4,LENT
	BHIS	G30
	MOV	R3,-(SP)
	TST	(4)+
	JMP	@(4)+
G03:	CALL	FILINP
	CMP	R1,#BUF
	BNE	G04
	TRAP	55.
G02:	MOV	DATINP,R0
	BEQ	1$
	BPL	G03
	TRAP	4
1$:	MOV	#'?,R0
.IF EQ,MASINA-BK0011
	CALL	ISVP
G00:	CALL	ISVP
.IFF
	CALL	ISV
G00:	CALL	ISV
.ENDC
	.BJSR	CURON
.IFT
	CALL	INBF0
	BCC	G5
.IFF
	.BJSR	IVEIL
.IFTF
G04:	MOV	#BUF,R3
	TRAP	112
	CMPB	@R3,#12
	BNE	G1
	TST	IODEV
	BLE	G5
	MOVB	@R3,IODEV+1
G1:	CMP	R4,@SP
	BEQ	G6
	TRAP	112
	CMPB	@R3,#',
	BNE	G13
	CMP	(R4)+,LENT
	BHIS	G12
	MOV	-(R4),R4
G12:	INC	R3
	BR	G1
G20:	MOV	#',,-(SP)
	MOV	R1,-(SP)
	CLR	R1
	MOV	R3,-(SP)
	CMPB	@R3,#'"
	BNE	22$
21$:	MOVB	(R3)+,4(SP)
	INC	(SP)
22$:	CMPB	@R3,#12
	BEQ	24$
	CMPB	(3)+,4(SP)
	BEQ	24$
	INC	R1
	BR	22$
24$:	MOV	R0,R5
	MOV	@SP,-(SP)
	MOV	R3,2(SP)
	BIT	#177400,R1
	BNE	28$
	CALL	TSTFRE
	MOV	FREBEG,R2
	MOV	(SP)+,R3
	MOV	R1,R0
	BEQ	26$
25$:	MOVB	(3)+,(2)+
	SOB	R1,25$
26$:	MOV	(SP)+,R3
	MOV	(SP)+,R1
	MOVB	R0,(R1)+
	TSTB	R5
	BMI	27$
	CLRB	(R1)+
27$:	MOVB	FREBEG,(R1)+
	MOVB	FREBEG+1,(R1)+
	SUB	R0,FRELEN
	MOV	R2,FREBEG
	CLRB	IODEV+1
	CMP	(SP)+,#',
	BEQ	G1
	BR	G40
28$:	TRAP	15.
G30:	MOV	(R4)+,R1
	MOV	R1,R0
G3:	MOV	-(R0),R0
.IF DF,SNG
	BIT	#20000,R0
	BNE	G20
.IFF
	BMI	G20
.IFTF
	MOV	R4,-(SP)
	MOV	R1,-(SP)
	CALL	CISLO
	BR	G41
	BR	G41
	BR	G41
G32:
.IFT
	BIT	#40000,R0
	BNE	32$
.IFF
	ROL	R0
	BMI	32$
.IFF
	ROL	R0
	BMI	31$
	JSR	R4,TODBL
	.WORD	SD,DXMOV,G33
	.WORD	ID,DXMOV,G33
.IFTF
31$:	JSR	R4,TOSNG
.IFF
	.WORD	IS,SXMOV,G33
.IFT
	.WORD	IR,SXMOV,G33
.IFTF
32$:	JSR	R4,TOINT
.IFF
	.WORD	SI,IXMOV,G33
.IFT
	.WORD	RI,IXMOV,G33
.IFTF
G5:	MOV	@SP,R4
G6:	CLR	SAVJMP
	TST	DATINP
	BEQ	1$
	BPL	G66
	MOV	R3,DATPTR
	BR	G66
1$:
	.BJSR	CUROFF
G66:	TST	(SP)+
	JMP	@(4)+
G33:	MOV	(SP)+,R4
G40:	TRAP	112
	CMPB	@R3,#',
	BEQ	G12
	CMPB	@R3,#12
	BEQ	G1
	TST	DATINP
	BGT	G1
G4:	TRAP	13.
	TST	(SP)+
	MOV	SAVJMP,R4
	JMP	@-2(R4)
G41:	TSTB	IODEV+1
	BEQ	1$
	CLR	R5
	MOV	#-1,R4
	CLRB	IODEV+1
	BR	G32
1$:	CMP	(SP)+,(SP)+
	BR	G4
GETARR::MOV	(SP)+,R1
	MOV	(SP)+,R3
	MOV	-6(R4),R0
	BR	G3
;
TOINT::	MOV	(SP)+,R2
.IFT
	BEQ	FROMS
.IFF
	BEQ	FROMD
	BGT	FROMS
.IFTF
	MOV	R5,@(SP)+
	BR	G33
.IFF
FROMD:	MOV	(SP)+,2(SP)
	MOV	(SP)+,2(SP)
	JMP	@(R4)+
;
E1:	
E2:
E3:	TRAP	6
	TRAP	5
;
.IFTF
TOSNG:	MOV	(SP)+,R2
;
.IFT
	BEQ	NOCONV
.IFF
	BMI	1$
	BNE	NOCONV
	ROL	4(SP)
	ADC	2(SP)
	ADC	@SP
	BVS	E2
	BCS	E2
	TST	(R4)+
	BR	FROMD
.IFTF
1$:	TST	-(R4)
.IFF
	BR	FROMI
;
TODBL:	TST	(SP)+
	BEQ	NOCONV
	BPL	FROMS
	CMP	(R4)+,(R4)+
.IFTF
FROMI:	MOV	R5,-(SP)
NOCONV:	TST	(R4)+
FROMS:	JMP	@(R4)+
;-------------------------------------------------
.IFF
DS::	ROL	4(SP)
	ADC	2(SP)
	ADC	(SP)
	BVS	1$
	BCS	1$
	MOV	(SP)+,2(SP)
	MOV	(SP)+,2(SP)
	JMP	@(R4)+
1$:	TRAP	6
;
.IFTF
NEG::	TST	@SP
	BEQ	1$
	ADD	#100000,@SP
1$:	JMP	@(R4)+
;
.IFT
IABS::	TST	@SP
	BPL	J1
;
INEG::	NEG	@SP
J1:	JMP	@(4)+
;
ISGN::	MOV	#1,R0
	TST	@SP
	BEQ	2$
	BPL	1$
	NEG	R0
1$:	MOV	R0,@SP
2$:	JMP	@(4)+
;
.ENDC
SUM::	ADD	(SP)+,@SP
SUMPAB:	BVS	OVF
	JMP	@(R4)+
OVF:	TRAP	6
;
SKIRT::	SUB	(SP)+,@SP
	BR	SUMPAB
;
.IF EQ,VM-1
;
SAND::	CLR	R0
	MOV	(SP)+,R2
	BGE	1$
	INC	R0
	NEG	R2
1$:	MOV	@SP,R1
	BGE	2$
	DEC	R0
	NEG	R1
2$:
	CALL	DAUG
	BCS	OVF
	TST	R0
	BEQ	3$
	NEG	R1
3$:	MOV	R1,@SP
	JMP	@(R4)+
;
IDIV::	MOV	(SP)+,R2
	MOV	@SP,R1
	CALL	DAL
	MOV	R1,@SP
	JMP	@(R4)+
;
MOD::	MOV	(SP)+,R2
	MOV	@SP,R1
	CALL	DAL
	MOV	R3,@SP
	JMP	@(R4)+
;
DAL::	CLR	R0
	MOV	R1,R3
	BPL	1$
	NEG	R3
	BMI	OVF
	INC	R0
1$:	TST	R2
	BEQ	2$
	BPL	3$
	NEG	R2
	BIS	#2,R0
3$:	MOV	R2,R5
	CLR	R1
5$:	CMP	R2,R3
	BHI	4$
	ASL	R2
	BR	5$
4$:	CMP	R2,R5
	BEQ	6$
	CLC
	ROR	R2
	ASL	R1
	CMP	R2,R3
	BHI	4$
	SUB	R2,R3
	INC	R1
	BR	4$
6$:	ASR	R0
	BCC	7$
	NEG	R1
	NEG	R3
7$:	TST	R0
	BEQ	8$
	NEG	R1
8$:	RETURN
2$:	TRAP	11.
;
.IFF
SAND::	MOV	(SP)+,R1
	MUL	@SP,R1
	BCS	OVF
	MOV	R1,@SP
	JMP	@(R4)+
;
IDIV::	MOV	(SP)+,R2
	MOV	@SP,R1
	SXT	R0
	DIV	R2,R0
	BCS	DIVNUL
	MOV	R0,@SP
	JMP	@(R4)+
;
MOD::	MOV	(SP)+,R2
	MOV	@SP,R1
	SXT	R0
	DIV	R2,R0
	BCS	DIVNUL
	MOV	R1,@SP
	JMP	@(R4)+
;
DIVNUL:	TRAP	11.
.ENDC
;
NOT::	COM	@SP
	JMP	@(R4)+
OR::	BIS	(SP)+,@SP
	JMP	@(R4)+
AND::	COM	@SP
	BIC	(SP)+,@SP
	JMP	@(R4)+
EQV::	COM	@SP
XOR::	MOV	(SP)+,R0
	XOR	R0,@SP
	JMP	@(R4)+
IMP::	COM	2(SP)
	BIS	(SP)+,@SP
	JMP	@(R4)+
;
;------------------------------------------------
ARR12:	TRAP	12.
;
INDEX::	MOV	(R4)+,R1
	MOV	(R1)+,R0
	BNE	1$
	TSTB	LYGIS
	BEQ	ARR12
.IF EQ,MASINA-BK0011
	BIT	#100,-4(1)
	BNE	ARR12
.IFTF
	MOV	ENDCOD,R0
	INC	R0
	BIC	#1,R0
	CMP	R0,CIKL
	BHIS	AR7
	MOV	#11.,@R0
	MOVB	#1,@R1
1$:	MOV	(R4)+,R3
	CMPB	R3,@R1
	BNE	AR9
.IFT
	BIT	#100,-4(1)
	BEQ	20$
	CALL	SETADR
20$:
.ENDC
	CLR	R1
2$:	CMP	@SP,(R0)+
	BHIS	AR9
	ADD	(SP)+,R1
	DEC	R3
	BLE	3$
	MOV	@R0,R2
.IF EQ,VM-1
	CALL	DAUG
.IFF
	MUL	R2,R1
.IFTF
	BR	2$
3$:
.IF EQ,MASINA-BK0011
	TST	SAVJMP
	BEQ	30$
	MOV	R4,-(SP)
	MOV	SAVJMP,R4
	CLR	SAVJMP
	CALL	SETPG1
30$:
.ENDC
	MOV	-4(R4),R3
	MOVB	3(R3),R2
.IFT
	CALL	DAUG2
.IFF
.IF EQ,MASINA-BK0011
	CALL	DAUG2
.IFF
	MUL	R2,R1
.ENDC
.ENDC
	ADD	R1,R0
.IF EQ,MASINA-BK0011
	CALL	VIRADR
.IFTF
	MOV	R0,-(SP)
	TST	(R3)+
	BEQ	4$
	JMP	@(R4)+
4$:	CMP	-(R4),-(R4)
;
ARR10::	MOV	#10.,-(SP)
;
ARRAY:: MOV	(R4)+,R1
	TST	@R1
	BNE	AR10
	MOV	@R4,R3
.IFT
	MOV	-(1),R1
	BIT	#100,R1
	BEQ	11$
	MOV	VIREND,R0
	CALL	SETADR
	BR	12$
11$:
.IFTF
	INC	ENDCOD
	BIC	#1,ENDCOD
	MOV	ENDCOD,R0
12$:	MOV	R3,R2
	ASL	R2
	ADD	R0,R2
.IFT
	BIT	#100,R1
	BEQ	13$
	TST	R2
	BPL	14$
	CALL	NEXPAG
	ADD	#40000,VIREND
	BIC	#37777,VIREND
	MOV	R5,R4
	BR	14$
13$:
.ENDC
	CMP	R2,CIKL
	BHIS	AR7
14$:	MOV	(SP)+,R1
	BMI	AR5
	INC	R1
	BVS	AR7
	MOV	R1,(R0)+
1$:	DEC	R3
	BEQ	AR2
	MOV	(SP)+,R2
	BLT	AR5
	INC	R2
	BVS	AR7
	MOV	R2,(R0)+
.IF EQ,VM-1
	CALL	DAUG
.IFF
	MUL	R2,R1
.IFTF
	BCC	1$
AR7:	TRAP	7
AR5:	TRAP	5
AR9:	TRAP	9.
AR10:	TRAP	10.
AR2:
.IF EQ,MASINA-BK0011
	TST	SAVJMP
	BEQ	1$
	MOV	R4,-(SP)
	MOV	SAVJMP,R4
	CLR	SAVJMP
	CALL	SETPG1
1$:
.ENDC
	MOV	-2(R4),R3
	MOVB	3(R3),R2
.IFT
	CALL	DAUG2
.IFF
.IF EQ,MASINA-BK0011
	CALL	DAUG2
.IFF
	MUL	R2,R1
.ENDC
.ENDC
	BCS	AR7
.IF EQ,MASINA-BK0011
	BIT	#100,-2(3)
	BEQ	13$
	MOV	(SP)+,R5
	MOV	R0,-(SP)
	MOV	R5,R0
	EMT	52
	MOV	(SP)+,R0
	MOV	R1,-(SP)
11$:	TST	R0
	BPL	12$
	CALL	NEXPAG
12$:	CLR	(0)+
	SOB	R1,11$
	TST	R0
	BMI	AR7
	CALL	SETPG1
	MOV	@R4,R1
	ADD	(SP)+,R1
	ASL	R1
	MOV	VIREND,(3)+
	ADD	R1,VIREND
	BR	4$
13$:
.IFTF
	ADD	R1,R0
	BCS	AR7
	CMP	R0,CIKL
	BHI	AR7
	MOV	ENDCOD,(R3)+
	MOV	R0,ENDCOD
	BIT	#400,@R3
	BEQ	3$
2$:	CLRB	-(R0)
	DEC	R1
	BGT	2$
	BR	4$
3$:	CLR	-(R0)
	SUB	#2,R1
	BGT	3$
4$:	MOVB	(4)+,@R3
	INC	R4
	CMP	R4,STRREG
	BHI	5$
	JMP	@(R4)+
5$:
ASTAR::
.IFT
	JMP	@#STARP
.IFF
	JMP	@#STAR
.IFTF
;
.IFT
SETPG1:	MOV	R0,-(SP)
	MOV	#1,R0
	EMT	52
	MOV	(SP)+,R0
	RETURN
;
NEXWRD:	ADD	#2,R3
	BVC	VEND
TSTPAG:	INC	R5
	CMP	R5,#5
	BLT	VEND
	CMP	R5,VIRSCR
	BGT	AR7
	BEQ	VEND
	INC	R5
VEND:	RETURN
;
NEXPAG:	CALL	TSTPAG
	MOV	R5,R0
	EMT	52
	MOV	#40000,R0
	RETURN	
;
SETADR:	MOV	R0,R2
	CLR	R5
1$:	INC	R5
	SUB	#40000,R2
	BCC	1$
	ADD	#100000,R2
	CALL	NEXPAG
	MOV	R2,R0
	MOV	R4,SAVJMP
	MOV	R5,R4
	RETURN
;
VIRADR:	BIT	#100,-2(3)
	BEQ	3$
	SUB	#40000,R0
	ADD	R1,R0
	MOV	2(SP),R5
1$:	SUB	#40000,R0
	BCS	2$
	CALL	TSTPAG
	BR	1$
2$:	MOV	R5,2(SP)
3$:	RETURN
;
DAUG2:	BIT	#100,-2(3)
	BEQ	DAUG
	ASR	R2
	BCC	DAUG
	MOV	#200,R2
.IFF
DAUG2:
.ENDC
.IF EQ,VM-2
DAUG::	MUL	R2,R1
	RETURN
.IFF
DAUG::	MOV	R1,R5
	CMP	R5,R2
	BLOS	3$
	MOV	R2,R5
	MOV	R1,R2
3$:	CLR	R1
4$:	TST	R5
	BEQ	7$
	TST	R2
	BMI	6$
	ASR	R5
	BCC	5$
	ADD	R2,R1
	BVS	6$
5$:	ASL	R2
	BR	4$
6$:	SEC
7$:	RETURN
.ENDC
;
.IF EQ,MASINA-BK0011
VIRPTR::MOV	(SP)+,R0
	MOV	(SP)+,R1
	BIC	#140000,R0
	DEC	R1
1$:	DEC	R1
	BLE	2$
	ADD	#40000,R0
	BCC	1$
	BIS	#140000,R0
2$:	MOV	R0,-(SP)
	JMP	@(4)+
;
.IF NDF,SNG
DXVIR::	MOV	#10,R1
	BR	XVIR
.IFTF
SXVIR::	MOV	#4,R1
	BR	XVIR
IXVIR::	MOV	#2,R1
XVIR:	MOV	(SP)+,R3
	MOV	(SP)+,R5
	SUB	R1,SP
	MOV	SP,R2
	ASR	R1
	BR	2$
1$:	CALL	NEXWRD
2$:	MOV	R5,-(SP)
	MOV	R3,-(SP)
	EMT	60
	MOV	R0,(2)+
	SOB	R1,1$
	JMP	@(4)+
;
.IFT
VDXMOV::MOV	#10,R1
	BR	VXMOV
.ENDC
VSXMOV::MOV	#4,R1
	BR	VXMOV
VIXMOV::MOV	#2,R1
VXMOV:	MOV	R1,R2
	ADD	SP,R2
	ASR	R1
	MOV	(2)+,R3
	MOV	(2)+,R5
	BR	2$
1$:	CALL	NEXWRD
2$:	MOV	(SP)+,R0
	MOV	R5,-(SP)
	MOV	R3,-(SP)
	EMT	120
	SOB	R1,1$
	CMP	(SP)+,(SP)+
	JMP	@(4)+
;
$XVIR::	MOV	@SP,R3
	MOV	2(SP),R5
	MOV	FREBEG,R2
	EMT	60
	CLR	R1
	BISB	R0,R1
	CMP	R1,FRELEN
	BHI	5$
	MOV	R2,-(SP)
	SUB	R1,FRELEN
	ADD	R1,FREBEG
	MOV	R1,-(SP)
	INC	R1
	BR	2$
1$:	CALL	NEXWRD
	MOV	R5,-(SP)
	MOV	R3,-(SP)
	EMT	60
	MOVB	R0,(2)+
2$:	DEC	R1
	BEQ	3$
	SWAB	R0
	MOVB	R0,(2)+
	SOB	R1,1$
3$:	JMP	@(4)+
5$:	TRAP	14.
;
V$XMOV::MOV	(SP)+,R1
	MOV	R1,R0
	MOV	(SP)+,R2
	MOV	@SP,R3
	MOV	2(SP),R5
	INC	R1
	BR	2$
1$:	CALL	NEXWRD
	MOV	R5,-(SP)
	MOV	R3,-(SP)
	CLR	R0
	BISB	(2)+,R0
2$:	DEC	R1
	BEQ	3$
	SWAB	R0
	BISB	(2)+,R0
	SWAB	R0
3$:	EMT	120
	DEC	R1
	BGT	1$
	JMP	@(4)+
.ENDC
.ENDC
	.END
